# Maintainer: KÃ¥re Hampf <khampf@users.sourceforge.net>
# Submitter: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=xmltv-huro
pkgver=0.5.65
pkgrel=1
pkgdesc="Set of utilities to download tv listings and format them in xml - huro grabber only"
arch=('any')
url="http://xmltv.org/wiki/"
license=('GPL')
depends=('perl-archive-zip' 'perl-datetime' 'perl-date-manip' 
  'perl-file-slurp' 'perl-datetime-format-strptime' 'perl-datetime-locale' 
  'perl-http-cache-transparent' 'perl-http-response-encoding' 
  'perl-html-tree' 'perl-io-stringy' 'perl-lingua-en-numbers-ordinate' 
  'perl-lingua-preferred' 'perl-soap-lite' 'perl-term-progressbar'
  'perl-term-readkey' 'perl-timedate' 'perl-tk-tablematrix' 
  'perl-unicode-string' 'perl-unicode-utf8simple' 'perl-www-mechanize'
  'perl-xml-dom' 'perl-xml-libxml' 'perl-xml-libxslt' 'perl-xml-simple'
  'perl-xml-twig' 'perl-xml-writer' 'perl-parse-recdescent')
_oname=xmltv
source=("http://downloads.sourceforge.net/${_oname}/${_oname}-${pkgver}.tar.bz2")
md5sums=('ead962e68507cbfb6edd9c23f79fc335')
provides=('xmltv')
conflicts=('xmltv')

prepare() {

	cd ${srcdir}/${_oname}-${pkgver}
# exclude all grabbers except tv_grab_huro
	read  exclude_grabbers <<-'LIST_END'
		tv_grab_ar,\
		tv_grab_ch_search,\
		tv_grab_dtv_la,\
		tv_grab_uk_rt,\
		tv_grab_uk_bleb,\
		tv_grab_uk_atlas,\
		tv_grab_uk_guardian,\
		tv_grab_uk_tvguide,\
		tv_grab_is,\
		tv_grab_it,\
		tv_grab_it_dvb,\
		tv_grab_na_dd,\
		tv_grab_fi,\
		tv_grab_fi_sv,\
		tv_grab_il,\
		tv_grab_es_laguiatv,\
		tv_grab_nl,\
		tv_grab_dk_dr,\
		tv_grab_se_swedb,\
		tv_grab_hr,\
		tv_grab_no_gfeed,\
		tv_grab_eu_egon,\
		tv_grab_se_tvzon,\
		tv_grab_fr,\
		tv_grab_fr_kazer,\
		tv_grab_pt,\
		tv_grab_pt_meo,\
		tv_grab_za,\
		tv_grab_eu_epgdata,\
		tv_grab_combiner,\
		tv_grab_na_dtv
LIST_END

# Avoid warning about already existing (generated) Makefile
	if [ -f Makefile ] ; then
		make distclean
	fi
	perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor --yes --exclude ${exclude_grabbers}
}

build() {
	cd ${srcdir}/${_oname}-${pkgver}
	make
}
package() {
  cd ${srcdir}/${_oname}-${pkgver}
  make DESTDIR=${pkgdir} PREFIX=/usr install

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
