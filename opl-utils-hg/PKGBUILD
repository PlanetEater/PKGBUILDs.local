# Maintainer: krz <krz@freemail.hu>
pkgname=opl-utils-hg
pkgver=0.9.3
pkgrel=1
pkgdesc="PC utilities from the Open PS2 Loader project"
arch=('any')
url="https://bitbucket.org/ifcaro/open-ps2-loader/wiki/Home"
license=('custom:AFL')
depends=()
makedepends=('mercurial')
provides=('opl-utils')
conflicts=('opl-utils')
source=('hg+https://bitbucket.org/ifcaro/open-ps2-loader'
	'001-omit_jobserver_warning.patch'
	'002-remove_missing_includes.patch')
sha256sums=('SKIP'
	'bff15adc33494b9634cdd70ac285a087846bb0a3e7abf8364d46cfbff134969a'
	'fb197251e596109b7e0e200187edecdba1e2576b9fd6c6f2361d8b305bf13400')
reponame=open-ps2-loader

prepare() {
	cd "${srcdir}/${reponame}"
	patch -Np0 -i "${srcdir}/001-omit_jobserver_warning.patch"
	patch -Np0 -i "${srcdir}/002-remove_missing_includes.patch"
}

build() {
	cd "${srcdir}/${reponame}"
	make pc_tools
}

package() {
	mkdir --parents "${pkgdir}/usr/bin"
	cd "${srcdir}/${reponame}"
	for _binfile in pc/*/bin/*; do
		msg " Installing ${_binfile}"
		cp -r ${_binfile} "${pkgdir}/usr/bin/"
	done
}

pkgver() {
	cd "${srcdir}/${reponame}"
	hg log -r . --template '{latesttag}-{latesttagdistance}-{node|short}' | sed -e 's/-/~/g'
}
